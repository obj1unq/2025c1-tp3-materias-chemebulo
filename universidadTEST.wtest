import universidad.*

describe "Inscripciones a materias - Pruebas de los requerimientos" {
    test "Registrar una materia aprobada por un estudiante indicando la nota obtenida." {
        const roque = new Alumno()
        const objetosI = new Materia()

        roque.aprobar(objetosI, 7)
        assert.that(roque.materiasAprobadas().contains(objetosI))
        assert.equals(7, roque.nota(objetosI))
    }

    test "De un estudiante se debería saber la cantidad de materias aprobadas, su promedio, y si tiene o no aprobada una materia" {
        const roque = new Alumno()
        const objetosI = new Materia()
        const objetosII = new Materia()
        const matematicaI = new Materia()

        assert.equals(0, roque.cantidadDeMateriasAprobadas())
        roque.aprobar(objetosI, 8)
        roque.aprobar(matematicaI, 8)
        assert.equals(2, roque.cantidadDeMateriasAprobadas())
        
        assert.equals(8, roque.promedio())

        assert.that(roque.materiasAprobadas().contains(objetosI))
        assert.notThat(roque.materiasAprobadas().contains(objetosII))
    }

    test "Si el estudiante ya tiene registrada la aprobación de la materia, lanza un error." {
        const roque = new Alumno()
        const objetosI = new Materia()
        const matematicaI = new Materia()

        roque.aprobar(objetosI, 8)
        assert.throwsException({ roque.aprobar(objetosI, 9) })
        assert.doesNotThrowException({ roque.aprobar(matematicaI, 10) })
    }
}